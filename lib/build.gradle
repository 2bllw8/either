/*
 * Copyright (c) 2021 2bllw8
 * SPDX-License-Identifier: Apache-2.0
 */
plugins {
    id 'java-library'
    id 'maven-publish'
    id 'pmd'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
}

group = 'exe.bbllw8'
version = '2.0.0'

javadoc {
    options.addStringOption('Xdoclint:all,-missing', '-quiet')
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.named('jar') {
    manifest {
        attributes(
                'Implementation-Title': rootProject.name,
                'Implementation-Version': project.version
        )
    }
}

tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = rootProject.name
            groupId = project.group
            version = project.version

            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Either'
                description = 'Implementation of the Either type for Java 8+'
                url = 'https://github.com/2bllw8/either'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
}

pmd {
    consoleOutput = true
    toolVersion = "6.38.0"
    ruleSets = [
            "category/java/errorprone.xml",
            "category/java/bestpractices.xml"
    ]
}
